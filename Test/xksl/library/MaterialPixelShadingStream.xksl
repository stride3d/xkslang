// Copyright (c) 2014-2017 Silicon Studio Corp. All rights reserved. (https://www.siliconstudio.co.jp)
// See LICENSE.md for full license information.
namespace SiliconStudio.Xenko.Rendering.Materials
{
    shader MaterialPixelShadingStream : MaterialPixelStream, LightStream
    {
        // Output of shading a material surface
        stage stream float3 shadingColor: SHADINGCOLOR;

        // Output of the shading color alpha
        stage stream float shadingColorAlpha: SHADINGCOLORALPHA;

        // Half vector (sum of normalWS + lightDirectionWS)
        stage stream float3 H: H;

        // normal dot half vector
        stage stream float NdotH: NDOTH;

        // light dot half vector
        stage stream float LdotH: LDOTH;

        // view dot half vector
        stage stream float VdotH: VDOTH;

        override void ResetStream()
        {
            base.ResetStream();
            streams.shadingColorAlpha = 1.0f;
        }

        // Computes material attributes per light
        stage void PrepareMaterialPerDirectLight()
        {
            // TODO: This is not plug-n-play
            // Used by microfacet
            streams.H = normalize(streams.viewWS + streams.lightDirectionWS);
            streams.NdotH = saturate(dot(streams.normalWS, streams.H));
            streams.LdotH = saturate(dot(streams.lightDirectionWS, streams.H));
            streams.VdotH = streams.LdotH;
        }
    };
}

